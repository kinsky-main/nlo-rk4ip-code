#version 460
#extension GL_EXT_shader_explicit_arithmetic_types_float64 : require
#extension GL_GOOGLE_include_directive : require

#include "nlo_complex_device.glslinc"

layout(local_size_x = 64) in;

layout(push_constant) uniform PushConstants {
    uint count;
    uint _pad;
    double scalar0;
    double scalar1;
} pc;

layout(set = 0, binding = 0, std430) buffer DstBuffer {
    nlo_vk_complex64 dst[];
};

void main()
{
    uint idx = gl_GlobalInvocationID.x;
    if (idx >= pc.count) {
        return;
    }

    const float max_log_float = 88.0f;
    const float min_log_float = -88.0f;

    float re_f = float(dst[idx].re);
    float im_f = float(dst[idx].im);
    if (re_f > max_log_float) {
        re_f = max_log_float;
    } else if (re_f < min_log_float) {
        re_f = min_log_float;
    }

    float exp_re = exp(re_f);
    dst[idx].re = double(exp_re * cos(im_f));
    dst[idx].im = double(exp_re * sin(im_f));
}
