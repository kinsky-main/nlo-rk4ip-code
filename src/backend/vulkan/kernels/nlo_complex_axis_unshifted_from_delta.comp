#version 460
#extension GL_EXT_shader_explicit_arithmetic_types_float64 : require
#extension GL_GOOGLE_include_directive : require

#include "nlo_complex_device.glslinc"

layout(local_size_x = 64) in;

layout(push_constant) uniform PushConstants {
    uint count;
    uint _pad;
    double scalar0;
    double scalar1;
} pc;

layout(set = 0, binding = 0, std430) buffer DstBuffer {
    nlo_vk_complex64 dst[];
};

const double NLO_TWO_PI = 6.283185307179586476925286766559;

void main()
{
    uint idx = gl_GlobalInvocationID.x;
    if (idx >= pc.count) {
        return;
    }
    if (!(pc.scalar0 > 0.0)) {
        dst[idx].re = 0.0;
        dst[idx].im = 0.0;
        return;
    }

    double step = NLO_TWO_PI / (double(pc.count) * pc.scalar0);
    uint positive_limit = (pc.count - 1u) / 2u;
    double value = 0.0;
    if (idx <= positive_limit) {
        value = double(idx) * step;
    } else {
        value = -(double(pc.count - idx) * step);
    }
    dst[idx].re = value;
    dst[idx].im = 0.0;
}
